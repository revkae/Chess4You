plugins {
    id 'java'
    id 'application'
}

mainClassName = 'me.raven.Main'

group 'me.raven'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

project.ext.lwjglVersion = "3.3.1"
project.ext.jomlVersion = "1.10.5"

// Auto-detect platform for LWJGL natives
import org.gradle.internal.os.OperatingSystem
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : osArch.contains("64") ? "natives-linux" : "natives-linux-x86"
        break
    case OperatingSystem.MAC_OS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("aarch64") || osArch.startsWith("arm")
                ? "natives-macos-arm64"
                : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64") ? "natives-windows" : "natives-windows-x86"
        break
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.platform:junit-platform-suite:1.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-bgfx"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nanovg"
    implementation "org.lwjgl:lwjgl-nuklear"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-par"
    implementation "org.lwjgl:lwjgl-shaderc"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-bgfx::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nanovg::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nuklear::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-shaderc::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
    implementation 'org.javatuples:javatuples:1.2'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
}

test {
    useJUnitPlatform()

    // macOS requires GLFW to run on the first thread
    if (OperatingSystem.current().isMacOsX()) {
        jvmArgs '-XstartOnFirstThread'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = false
        showExceptions = true
        showCauses = true
        showStackTraces = true

        // Show individual test results
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest Results: ${result.resultType} " +
                        "(${result.testCount} tests, " +
                        "${result.successfulTestCount} passed, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped)"
            }
        }
    }
}

run {
    // macOS requires GLFW to run on the first thread
    if (OperatingSystem.current().isMacOsX()) {
        jvmArgs '-XstartOnFirstThread'
    }
}